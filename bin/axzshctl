#!/usr/bin/env zsh
#
# AX-ZSH: Alex' Modular ZSH Configuration
# Copyright (c) 2015-2021 Alexander Barton <alex@barton.de>
#

# Embedded ax-common compatibility functions ...
function ax_msg {
	case "$1" in
		"0")	c="32";	;;
		"1")	c="33";	;;
		"2")	c="31";	;;
		"-")	c="1";	;;
		*)	c="0";	;;
	esac
	shift
	printf "\e[${c}m%s\e[0m\n" "$@"
}
function ax_error {
	ax_msg 2 "$@" >&2
}

function Version {
	echo "ax-zsh -- Modular configuration system for the Z shell (ZSH)"
	echo "Copyright (c) 2015-2019 Alexander Barton <alex@barton.de>."
	echo "Licensed under the terms of the MIT license, see LICENSE.md for details."
	echo "Homepage: <https://github.com/alexbarton/ax-zsh>"
	if [[ -d "$AXZSH/.git" && -n "$commands[git]" ]]; then
		echo -n "Version: Git ID "
		( cd "$AXZSH" && git describe --always )
	fi
	echo
	exit 0
}

function Usage {
	echo "Usage: $NAME <command> [...]"
	echo
	echo "  enable"
	echo "    Enable AX-ZSH altogether."
	echo "  disable"
	echo "    Disable AX-ZSH altogether."
	echo
	echo "  enable-plugin <name|directory> [<name|directory> [...]]"
	echo "    Enable plugin(s)."
	echo "  disable-plugin <name> [<name> [...]]"
	echo "    Disable plugin(s)."
	echo "  list-enabled"
	echo "    List enabled plugins."
	echo
	echo "  reset-plugins"
	echo "    Reset active plugins to the default set."
	echo "  enable-default-plugins"
	echo "    Enable all default plugins."
	echo "  check-plugins"
	echo "    Detect plugins which are \"useful\" on this system."
	echo
	echo "  set-theme <name>|-"
	echo "    Set active theme to <name>, or to the default."
	echo
	echo "  upgrade"
	echo "    Upgrade AX-ZSH installation (requires Git)."
	echo "  update-caches"
	echo "    Force rebuild of all cache files."
	echo
	exit 0
}

function UpdatePluginCache {
	[[ -r "$AXZSH/cache" ]] || return 0

	[[ "$1" = "-v" ]] && ax_msg - "Invalidating & updating caches ..."

	if [[ -d "$ZSH_CACHE_DIR" ]]; then
		[[ "$1" = "-v" ]] && echo "Removing ZSH cache folder ..."
		rm -fr "$ZSH_CACHE_DIR"
	fi

	[[ "$1" = "-v" ]] && echo "Removing AX-ZSH cache files ..."
	rm -rf \
		$AXZSH/cache/ax-io.cache \
		$AXZSH/cache/zlogin.cache \
		$AXZSH/cache/zlogout.cache \
		$AXZSH/cache/zprofile.cache \
		$AXZSH/cache/zshrc.cache \
		|| return 1

	echo "Regenerating AX-ZSH cache ..."
	AXZSH_PLUGIN_CHECK=1 zsh -ilc '' >/dev/null
}

function NormalizedPluginName {
	if [[ "$1" =~ "^[[:alnum:]-]+/[[:alnum:]_.-]+$" ]]; then
		echo "${1:gs/\//#}"
	elif [[ "$1" =~ "/" ]]; then
		echo "${1:t}"
	else
		echo "$1"
	fi
}

function EnableAXZSH {
	for f (~/.zlogin ~/.zlogout ~/.zprofile ~/.zshrc); do
		ln -s "$AXZSH/ax.zsh" "$f" \
			|| ax_error "Failed to create symbolic link for \"$f\"!"
	done
}

function DisableAXZSH {
	for f (~/.zlogin ~/.zlogout ~/.zprofile ~/.zshrc); do
		if [[ -h "$f" ]]; then
			rm "$f" || ax_msg 2 "Failed to remove \"$f\"!"
		elif [[ -e "$f" ]]; then
			ax_error "Error: Not removing \"$f\", it is not a symbolic link!"
		else
			ax_msg 1 "Warning: \"$f\" already does not exist. Ok."
		fi
	done
}

function EnablePlugin {
	local plugin=$(NormalizedPluginName "$1")
	local dir="$AXZSH/active_plugins"

	if [[ -h "$dir/$plugin" ]]; then
		ax_msg 1 "Plugin \"$1\" already active!"
		return 1
	fi

	if [[ "$1" =~ "^[[:alnum:]-]+/[[:alnum:]_.-]+$" ]]; then
		# GitHub plugin
		mkdir -p "$AXZSH/repos"
		if [[ ! -e "$AXZSH/repos/$plugin" ]]; then
			ax_msg - "Cloning module from GitHub ..."
			git clone --depth=1 "https://github.com/$1.git" \
			 "$AXZSH/repos/$plugin" \
				|| ax_error "Failed to clone repository!"
		fi
		# Try to enable a theme in this "foreign module", but ignore
		# errors: we don't know if this module provides a theme or is
		# a "regular" plugin ...
		if SetTheme "$plugin" 2>/dev/null; then
			ax_msg 0 "Module \"$1\" was enabled as theme \"${plugin#*#}\"."
			# A theme was enabled: So assume that this is a theme
			# and don't enable it as plugin.
			return 0
		fi
		echo "Trying to enable \"$1\" as plugin ..."
	elif ! [[ "$1" =~ "^[[:alnum:]_.-]+$" ]]; then
		ax_error "Invalid plugin name!"
		return 1
	fi

	for dname (
		"$plugin:A"
		"$AXZSH_PLUGIN_D/$plugin"
		"$ZSH_CUSTOM/$plugin"
		"$AXZSH/custom_plugins/$plugin"
		"$AXZSH/repos/$plugin"
		"$AXZSH/plugins/$plugin"
		"$AXZSH/default_plugins/$plugin"
		"$AXZSH/core/$plugin"
	); do
		[[ ! -d "$dname" ]] && continue
		mkdir -p "$dir"
		if ! (
			cd "$dir" || exit 9
			ln -s "$dname" "$PWD"
		); then
			ax_error "Failed to create link!"
			return 1
		fi
		ax_msg 0 "Plugin \"$1\" enabled."
		return 0
	done

	ax_error "Plugin \"$1\" not found!"
	return 1
}

function DisablePlugin {
	local plugin=$(NormalizedPluginName "$1")
	local dir="$AXZSH/active_plugins"
	local r=-1

	# Active theme?
	if [[ $(readlink "$AXZSH/active_theme") = "$AXZSH/repos/$plugin/"* ]]; then
		rm "$AXZSH/active_theme"; r=$?
	fi

	# Active plugin?
	if [[ -h "$dir/$plugin" ]]; then
		rm "$dir/$plugin"; r=$?
	fi

	if [[ $r -eq -1 ]]; then
		ax_msg 1 "Plugin \"$1\" not active, nothing to do?"
		r=1
	fi

	if [[ "$plugin" = *"#"* ]]; then
		# Name matches a cloned repository, try to clean up!
		echo "Cleaning up cloned repository ..."
		rm -fr "$AXZSH/repos/$plugin"
	fi

	return $r
}

function ListEnabledPlugins {
	for plugin ($AXZSH/active_plugins/*(N)); do
		print ${plugin:t:s/#/\//}
	done
	return 0
}

function ResetPlugins {
	local dir="$AXZSH/active_plugins"
	local r1=0, r2=0

	if [[ -e "$dir" ]]; then
		ax_msg - "Removing all symbolic links in $dir ..."
		find "$dir" -type l -print -delete; r1=$?
	fi

	ax_msg - "Removing all external repositories in \"$AXZSH/repos\" ..."
	rm -fr "$AXZSH/repos"; r2=$?

	[[ $r1 == 0 && $r2 == 0 ]] && return 0 || return 1
}

function EnableDefaultPlugins {
	local dir="$AXZSH/active_plugins"

	ax_msg - "Activating default plugins ..."
	mkdir -p "$dir"
	(
		cd "$dir" || exit 9
		ln -sf "$AXZSH/default_plugins/"* "$PWD"
	)
	return $?
}

function SetTheme {
	local link_name="$AXZSH/active_theme"

	# --- Powerlevel10k ---
	# Remove "instant prompt" configuration, if any ...
	rm -f "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"

	if [[ "$1" = "-" ]]; then
		rm -f "$link_name" || return 1
		ax_msg 0 "Theme settings have been reset."
		return 0
	fi

	if [[ -r "$1" ]]; then
		theme="$1"
	elif [[ -r "$AXZSH/custom_themes/$1.axzshtheme" ]]; then
		theme="$AXZSH/custom_themes/$1.axzshtheme"
	elif [[ -r "$AXZSH/themes/$1.axzshtheme" ]]; then
		theme="$AXZSH/themes/$1.axzshtheme"
	else
		# Look for theme in specific remote module:
		for f (
			"$AXZSH/repos/$1/"*.axzshtheme(N[1])
			"$AXZSH/repos/$1/"*.zsh-theme(N[1])
		); do
			if [[ -r "$f" ]]; then
				theme="$f"
				break
			fi
		done

		# Look for theme inside of installed plugins:
		for dname (
			"$AXZSH/custom_themes"
			"$AXZSH/custom_plugins/"*(N)
			"$AXZSH/repos/"*(N)
		); do
			if [[ -r "$dname/$1.axzshtheme" ]]; then
				theme="$dname/$1.axzshtheme"
				break
			elif [[ -r "$dname/$1.zsh-theme" ]]; then
				theme="$dname/$1.zsh-theme"
				break
			fi
		done

		if [[ -z "$theme" ]]; then
			ax_error "Theme \"$1\" not found!"
			return 1
		fi
	fi
	ln -fs "$theme" "$link_name" || return 1
	return $?
}

function UpgradeAXZSH {
	if [[ $+commands[git] -eq 0 ]]; then
		ax_error "The git(1) command is not available!"
		return 1
	fi
	if [[ ! -d "$AXZSH/.git" ]]; then
		ax_error "AX-ZSH seems not to be installed using Git. Can't upgrade!"
		return 1
	fi

	ax_msg - "Upgrading AX-ZSH in \"$AXZSH\" using git(1) ..."
	(
		set -e
		cd "$AXZSH"
		git pull --ff-only || ax_error "Git pull failed!"
		git log --pretty=format:"%C(yellow)%h %C(blue)%ar %C(green)%an %Creset%s" ORIG_HEAD..
	)
}

function UpgradeForeignPlugins {
	if [[ $+commands[git] -eq 0 ]]; then
		ax_error "The git(1) command is not available!"
		return 1
	fi

	for dir ($AXZSH/repos/*(N)); do
		name=${dir:t:s/#/\//}
		if [[ -d "$dir/.git" ]]; then
			ax_msg - "Upgrading \"$name\" [git] ..."
			(
				set -e
				cd "$dir"
				git pull --ff-only || ax_error "Git pull failed!"
				git log --pretty=format:"%C(yellow)%h %C(blue)%ar %C(green)%an %Creset%s" ORIG_HEAD..
			)
		else
			ax_error "Unknown repository type!"
		fi
	done
}

function CheckPlugins {
	missing_plugins=()
	invalid_plugins=()

	# Building cache file for all zshrc core files:
	if ! T=$(mktemp); then
		ax_error "Failed to create temporary file!"
		return 1
	fi
	for p in $AXZSH/core/*/*.zshrc; do
		[[ "$(basename "$p")" == "01_zprofile.zshrc" ]] && continue
		printf "# BEGIN: %s\nax_plugin_init()\n{\n" "$p" >>"$T"
		cat "$p" >>"$T"
		printf "}\nax_plugin_init\n# END: %s\n\n" "$p" >>"$T"
	done

	ax_msg - "Checking plugins ..."
	for dir ($AXZSH/plugins/[a-z0-9]*(N)); do
		plugin=${dir:t}

		# Test if plugin is already enabled
		if [[ -e "$AXZSH/active_plugins/$plugin" ]]; then
			printf ' \e[1;32m+\e[m "\e[1m%s\e[m" ... ' "${plugin}"
			enabled=1
		else
			printf ' \e[1;31m-\e[m "%s" ... ' "${plugin}"
			unset enabled
		fi

		# Test plugin ...
		new_plugin=""
		for script ($AXZSH/plugins/$plugin/$plugin.{zshrc,zprofile,ax-io}); do
			[[ -r "$script" ]] || continue
			(
				AXZSH_PLUGIN_CHECK=1
				source "$T"
				ax_plugin_fnc() { source "$script" }
				ax_plugin_fnc
			); r=$?
			[[ $r -eq 0 ]] && new_plugin=$plugin
			break
		done
		if [[ -n "$new_plugin" ]]; then
			detected_plugins+=($new_plugin)
			[[ -n "$enabled" ]] || missing_plugins+=($new_plugin)
			ax_msg 0 "OK."
		elif [[ $r -eq 91 ]]; then
			ax_msg 1 "ignored."
		elif [[ $r -eq 92 ]]; then
			ax_msg 1 "optional."
		else
			[[ -n "$enabled" ]] && invalid_plugins+=($plugin)
			ax_msg 2 "failed ($r)."
		fi
	done
	rm -f "$T"
	echo

	result=0
	if [[ -n "$missing_plugins" ]]; then
		ax_msg 1 "Run the following command to enable all missing plugins:"
		echo "$AXZSH/bin/axzshctl enable-plugin" $missing_plugins
		echo
		result=1
	else
		ax_msg 0 "All detected plugins are already enabled."
	fi

	if [[ -n "$invalid_plugins" ]]; then
		ax_msg 1 "Run the following command to disable all failed plugins:"
		echo "$AXZSH/bin/axzshctl disable-plugin" $invalid_plugins
		result=1
	else
		ax_msg 0 "No failed plugins are enabled."
	fi

	echo
	return $result
}

NAME="$0:t"

[[ $# -gt 0 ]] || Usage

if [[ -z "$AXZSH" || ! -r "$AXZSH/ax.zsh" ]]; then
	[[ -r "$HOME/.axzsh/ax.zsh" ]] && AXZSH="$HOME/.axzsh"
	if [[ ! -r "$AXZSH/ax.zsh" ]]; then
		ax_error "Oops, \"AXZSH\" is not set or invalid and can't be autodetected!"
		exit 3
	fi
fi

cmd="$1"
shift

case "$cmd" in
	"enable")
		[[ $# -eq 0 ]] || Usage
		EnableAXZSH
		;;
	"disable")
		[[ $# -eq 0 ]] || Usage
		DisableAXZSH
		;;
	"enable-plugin")
		[[ $# -gt 0 ]] || Usage
		for plugin in "$@"; do
			EnablePlugin "$plugin"
		done
		UpdatePluginCache
		;;
	"disable-plugin")
		[[ $# -gt 0 ]] || Usage
		for plugin in "$@"; do
			DisablePlugin "$plugin"
		done
		UpdatePluginCache
		;;
	"list-enabled")
		[[ $# -eq 0 ]] || Usage
		ListEnabledPlugins
		;;
	"reset-plugins")
		[[ $# -eq 0 ]] || Usage
		ResetPlugins
		EnableDefaultPlugins
		UpdatePluginCache
		;;
	"enable-default-plugins")
		[[ $# -eq 0 ]] || Usage
		EnableDefaultPlugins && UpdatePluginCache
		;;
	"check-plugins")
		[[ $# -eq 0 ]] || Usage
		CheckPlugins
		;;
	"set-theme")
		[[ $# -eq 1 ]] || Usage
		SetTheme "$1"
		;;
	"upgrade")
		[[ $# -eq 0 ]] || Usage
		UpgradeAXZSH
		UpgradeForeignPlugins
		UpdatePluginCache
		;;
	"update-caches")
		[[ $# -eq 0 ]] || Usage
		UpdatePluginCache -v
		;;
	"--version"|"version")
		Version >&2
		;;
	"--help"|"help")
		Usage >&2
		;;
	*)
		ax_error "Invalid command \"$cmd\"!"
		ax_error "Try \"$0 --help\" for more information."
		exit 2
esac
