#!/usr/bin/env zsh
#
# AX-ZSH: Alex' Modular ZSH Configuration
# Copyright (c) 2015-2024 Alexander Barton <alex@barton.de>
#

source "$AXZSH/core/11_terminal/11_terminal.zshrc" || exit 1

unset VERBOSE ONE_LINE

while [[ $# -gt 0 ]]; do
	case "$1" in
		"-1")
			ONE_LINE=1
			;;
		"-f")
			FEATURE_TEST=1
			;;
		"-v")
			VERBOSE=1
			;;
		*)
			echo "Usage: ttyinfo [-1] [-f] [-v]" >&2
			exit 1
	esac
	shift
done

print -Pn -- "$fg[white]$FX[bold]$SHORT_HOST$FX[no-bold] "
print -Pn -- "$fg[yellow]$(tty)$FX[reset], "
print -Pn -- "$fg[green]$FX[underline]${TERM:-?}$FX[reset] "
print -Pn -- "(${COLUMNS:-?}x${LINES:-?})"
[[ -n "$TERM_DOWNGRADED_FROM" ]] && print -Pn -- " [$TERM_DOWNGRADED_FROM]"
[[ -n "$TERM_PROGRAM" ]] && print -Pn -- "; $fg[green]$TERM_PROGRAM$FX[reset]"
[[ -n "$$TERM_PROGRAM_VERSION" ]] && print -Pn -- " $TERM_PROGRAM_VERSION"
print -Pn -- "$FX[reset]"
echo

[[ -n "$ONE_LINE" ]] && return 0

typeset -i max=${COLUMNS:-80}
typeset -i col=0
for key (
	LANG
	LC_ALL
	LC_ADDRESS
	LC_COLLATE
	LC_CTYPE
	LC_IDENTIFICATION
	LC_MEASUREMENT
	LC_MESSAGES
	LC_MONETARY
	LC_NAME
	LC_NUMERIC
	LC_PAPER
	LC_TELEPHONE
	LC_TIME
	-
	COLORTERM
	CLICOLOR
	TERM_COLORS
	VTE_VERSION
	-
	DISPLAY
); do
	if [[ $key == "-" ]]; then
		[[ $col -gt 0 ]] && echo
		col=0
		continue
	fi

	[[ "${(P)key-UNSET}" = "UNSET" ]] && continue
	val=${(P)key}

	col=$(( $col + ${#key} + ${#val} + 5 ))
	if [[ $col -gt $max ]]; then
		echo
		col=$(( ${#key} + ${#val} + 5 ))
	fi

	print -Pn -- "$key=\"$fg[cyan]$val$FX[reset]\"; "
done
[[ $col -gt 0 ]] && echo

feature_tests="
Styles:		Normal
		\033[1mBold\033[0m
		\033[2mDim\033[0m
		\033[3mItalic\033[0m
		\033[3;1mBold italic\033[0m
		\033[4mUnderline\033[0m (with \033[58;5;5m\033[4mcolor\033[0m)
		\033[4:2mDouble underline\033[0m (\033[21mvariant 2\033[0m)
		\033[4:3mCurly underline\033[0m
		\033[4:4mDotted underline\033[0m
		\033[4:5mDashed underline\033[0m
		\033[5mBlink\033[0m
		\033[7mReverse\033[0m
		Hidden text: \"\033[8mHIDDEN\033[0m\"
		\033[9mStrikethrough\033[0m
		\033[53mOverline\033[0m
		\033[74mSubscript\033[0m
		\033[31mRed\033[0m, \033[92mbright green\033[0m, black on \033[30;46mblue\033[0m, ...
		\033]8;;https://github.com/alexbarton/ax-zsh\033\\Hyperlink\033]8;;\033\\

Ligatures:	->  <-  !=  ==  === !== >=  <=  =>

Nerdfont:	         󰾆      󰢻   󱑥   󰒲   󰗼
Fontawesome:	                        
"
[[ -n "$FEATURE_TEST" ]] && printf "%b\n" "${feature_tests}"

[[ -z "$VERBOSE" ]] && return 0

check_function_result() {
	"$1"; r=$?
	print -Pn -- " - $fg[white]$1$FX[reset]() = "
	if [[ $r -eq 0 ]]; then
		print -P -- "$fg[green]yes$FX[reset]"
	else
		print -P -- "$fg[red]NO$FX[reset]"
	fi
	return $r
}

[[ -z "$FEATURE_TEST" ]] && echo
for f (
	axzsh_is_dumb_terminal
	axzsh_is_modern_terminal
	axzsh_is_utf_terminal
	axzsh_is_widechar_terminal
); do
	check_function_result "$f"
done
echo
