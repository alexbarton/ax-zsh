#!/usr/bin/env zsh
#
# AX-ZSH: Alex' Modular ZSH Configuration
# Copyright (c) 2015-2022 Alexander Barton <alex@barton.de>
#

source "$AXZSH/core/11_terminal/11_terminal.zshrc" || exit 1

unset VERBOSE

while [[ $# -gt 0 ]]; do
	case "$1" in
		"-v")
			VERBOSE=1
			;;
		*)
			echo "Usage: ttyinfo [-v]" >&2
			exit 1
	esac
	shift
done

print -Pn -- "$fg[white]$FX[bold]$HOST$FX[no-bold] "
print -Pn -- "$fg[yellow]$(tty)$FX[reset], "
print -Pn -- "$fg[green]$FX[underline]${TERM:-?}$FX[reset] "
print -Pn -- "(${COLUMNS:-?}x${LINES:-?})"
print -Pn -- "$FX[reset]"
[[ -n "$LANG" ]] && print -Pn -- "; LANG=$LANG"
[[ -n "$COLORTERM" ]] && print -Pn -- "; COLORTERM=$COLORTERM"
echo

[[ -z "$VERBOSE" ]] && return 0

check_function_result() {
	"$1"; r=$?
	print -Pn -- " - $fg[white]$1$FX[reset]() = "
	if [[ $r -eq 0 ]]; then
		print -P -- "$fg[green]yes$FX[reset]"
	else
		print -P -- "$fg[red]NO$FX[reset]"
	fi
	return $r
}

echo
for f (
	axzsh_is_dumb_terminal
	axzsh_is_modern_terminal
	axzsh_is_utf_terminal
	axzsh_is_widechar_terminal
); do
	check_function_result "$f"
done
echo
